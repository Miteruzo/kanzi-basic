;*******************************************************************************
; ワﾟイル名  : main.hsp
; 更新年月日 : 平30/06/08
; 作成者     : Taker32X
; 作成年月日 : 平30/04/24
; 機能       : 漢字 BASIC のインタプリタ
;*******************************************************************************

;------------------------------------ TODO ------------------------------------

;------------------------------------------------------------------------------

#const MAJOR_VER 0
#const MINOR_VER 0
#const PATCH_VER 0
#const BUILD_NUM 388
#define PROJECT_NAME ("kb")
#define PROJECT_FILE ("main.hsp")

#addition "../mod_taker.as

#uselib "user32
#cfunc GetWindowLong "GetWindowLongA" int,int
#func SetWindowLong "SetWindowLongA" int,int,int
#cfunc IsZoomed "IsZoomed" int
#cfunc IsIconic "IsIconic" int

#uselib "kernel32
#func Beep "Beep" int,int

#uselib "gdi32
#func GetTextExtentPoint32 "GetTextExtentPoint32A" int,var,int,var

#uselib "imm32
#cfunc ImmCreateContext "ImmCreateContext"
#cfunc ImmAssociateContext "ImmAssociateContext" int,int
#func ImmSetCompositionWindow "ImmSetCompositionWindow" int,var,int
#func ImmGetCompositionString "ImmGetCompositionStringA" int,int,var,int
#func ImmSetCompositionFont "ImmSetCompositionFontA" int,var
#func ImmReleaseContext "ImmReleaseContext" int,int
#func ImmDestroyContext "ImmDestroyContext" int
#cfunc ImmGetContext "ImmGetContext" int

#const SIZE_X 8
#const SIZE_Y 16

#const MAX_X 240
#const MAX_Y 80

#const KEY_RETURN $0D

#define ctype InKey(%1) If ((key_(%1)==0||key_(%1)>=32)&&key_(%1)\3==0&&key(%1))

#enum WINDOW_MAIN=0

#module

#deffunc OnDisplay str p1
	work=p1
	Repeat StrLen(work)
		If cursor_x@+cnt>=(ginfo_winx/SIZE_X@){
			cursor_x@=-cnt
			cursor_y@+
		}
		data@(cnt+cursor_x@,cursor_y@)=Peek(work,cnt)or(color_r@<<8)or(color_g@<<16)or(color_b@<<24)
	Loop
	Return

#defcfunc Kan2Arabia str p1
	line_1st=1
	line_num=0
	reading=p1
	For j,0,StrLen(reading),2
		Repeat 9
			If StrMid(reading,j,2)==number@(cnt){
				line_1st=cnt+1
				Break
			}
		Loop
		Repeat 3
			If StrMid(reading,j,2)==number@(cnt+9){
				line_num+(line_1st*(cnt+1)+(line_1st==0))*10
				line_1st=0
				Break
			}
		Loop
		Repeat 3
			If StrMid(reading,j,2)==number@(cnt+12){
				line_num+(line_1st+(line_1st==0))*Int(PowF(10,cnt+2))
				line_1st=0
				Break
			}
		Loop
	Next
	Return line_num+line_1st

#defcfunc GetKey2 int p1
	GetKey key,p1
	Return key

#global

	Screen WINDOW_MAIN,Limit(ginfo_dispx,SIZE_X,MAX_X*SIZE_X),Limit(ginfo_dispy,SIZE_Y,MAX_Y*SIZE_Y),SCREEN_HIDE
	SetWindowLong hwnd,-16,GetWindowLong(hwnd,-16)or$50000
	Width 640,400
	gosa_x=640-ginfo_winx
	gosa_y=400-ginfo_winy
	Width 640+gosa_x,400+gosa_y
	hwnd_ime=ImmGetContext(hwnd)
	Dim key,256
	Dim data,MAX_X,MAX_Y
	sDim program,256,65529
	number="一","二","三","四","五","六","七","八","九","十","廿","卅","百","千","萬"
	through="　"," ","，
	ClS 4
	gSel WINDOW_MAIN,1
	Dim logfont,20
	logfont(0)=SIZE_Y
	Poke logfont,23,1
	font_name=MSMINCHO
	MemCpy logfont,font_name,strlen(font_name),28,0
	ImmSetCompositionFont hwnd_ime,logfont
	Font MSMINCHO,SIZE_Y
	color_r=0:color_g=255:color_b=255
	OnDisplay "漢字 BASIC −培基君− ("+VERSION_NUMBER+" 號)
	cursor_y+
	OnDisplay "(C)松田有著作權於平成三十年
	cursor_y+
	OnDisplay "能用 "+VarSize(program)*Length(program)+" 字
	cursor_y+
	color_r=255:color_g=255:color_b=255
	OnDisplay "準備好。
	cursor_x=0:cursor_y+2

*Main
	ime_text_="
	Repeat
		If IsIconic(hwnd)==0{
			If ginfo_winy<SIZE_Y{
				Width ginfo_winx+gosa_x,SIZE_Y+gosa_y
			}
			If IsZoomed(hwnd)==0{
				work_x=ginfo_winx:work_y=ginfo_winy
				If work_x\SIZE_X{
					work_x=(work_x+SIZE_X/2)/SIZE_X*SIZE_X
				}
				If work_y\SIZE_Y!=0{
					work_y=(work_y+SIZE_Y/2)/SIZE_Y*SIZE_Y
				}
				If ginfo_winx!=work_x||ginfo_winy!=work_y{
					Width work_x+gosa_x,work_y+gosa_y
				}
			}
			ReDraw 0
			Color 0,0,0
			BoxF
			form=2,cursor_x*SIZE_X,cursor_y*SIZE_Y
			ImmSetCompositionWindow hwnd_ime,form
			ime_text_=ime_text
			ime_text="
			ime_text="
			ImmGetCompositionString hwnd_ime,8,ime_text,str_size2
			str_size2=stat
			sDim ime_text,str_size2+1
			ImmGetCompositionString hwnd_ime,8,ime_text,str_size2
			If run_mode{
				Repeat
					If run_line>=65529{
						OnDisplay "好。
						cursor_y+
						run_mode=0
						run_line=0
						Break
					}
					If program(run_line)==""{
						run_line+
					}Else{
						Break
					}
				Loop
				If run_mode==0{
					Continue
				}
				Split program(run_line),"，",sentence
				stat_=stat
				work_line=sentence(0)
				sentence=sentence(1)
				Repeat stat_-2,2
					sentence+"，"+sentence(cnt)
				Loop
				GoSub *Check
				If error{
					Break
				}
				If GetKey2($11)and GetKey2('C'){
					OnDisplay "壞了於"+work_line+"。
					cursor_x=0
					cursor_y+
					OnDisplay "好。
					cursor_y+
					run_mode=0
				}
				run_line+
			}Else{
				Repeat 256
					If key(cnt){
						key_(cnt)+
					}Else{
						key_(cnt)=0
					}
					GetKey key(cnt),cnt
					If ginfo_act!=WINDOW_MAIN{
						key(cnt)=0
					}
					If ime_text_==""&&ime_text==""{
						If (key_(cnt)==0||key_(cnt)>=32)&&key_(cnt)\3==0&&('0'<=cnt&&cnt<='Z'||cnt==$20)&&key(cnt)!=0&&key(243)!=0{
							Pos cursor_x*SIZE_X,cursor_y*SIZE_Y
							data(cursor_x,cursor_y)=(cnt+('A'<=cnt&&cnt<='Z'&&key($10)==0)*('a'-'A'))or(color_r<<8)or(color_g<<16)or(color_b<<24)
							cursor_x+
							If cursor_x>=ginfo_winx/SIZE_X{
								cursor_x=0
								cursor_y+
							}
						}
						If key(cnt)&&(key_(cnt)==0||key_(cnt)>=48)&&($08<=cnt&&cnt<=$7F){
							blink=0
						}
					}
				Loop
				If ime_text_==""&&ime_text==""{
					InKey(KEY_RETURN){
						sentence="
						Repeat MAX_X
							If (data(cnt,cursor_y)and$FF)==0{
								Break
							}
							sentence+StrF("%c",data(cnt,cursor_y)and$FF)
						Loop
						cursor_x=0
						cursor_y+
						Split sentence,"「",work_a
						stat_=stat
						ForEach through
							StrRep work_a(0),through(cnt),""
						Loop
						sentence=work_a(0)
						Repeat stat_-1,1
							Split work_a(cnt),"」",work_b
							stat_=stat
							work_a(cnt)=work_b(0)
							cnt__=cnt
							Repeat stat_-1,1
								cnt_=cnt
								ForEach through
									StrRep work_b(cnt_),through(cnt),""
								Loop
								work_a(cnt__)+"」"+work_b(cnt)
							Loop
							sentence+"「"+work_a(cnt)
						Loop
						If sentence!=""{
							GoSub *Check
						}
					}Else: InKey($08){
						cursor_x-
						If cursor_x<0: cursor_x=0
						Poke data,(cursor_x+cursor_y*MAX_X)*4,0
					}Else: InKey($25){
						cursor_x-
						If cursor_x<0{
							If cursor_y>0{
								cursor_x=ginfo_winx/SIZE_X-1
								cursor_y-
							}Else{
								cursor_x=0
							}
						}
					}Else: InKey($26){
						cursor_y-
						If cursor_y<0{
							cursor_y=0
						}
					}Else: InKey($27){
						cursor_x+
						If cursor_x>=ginfo_winx/SIZE_X{
							cursor_x=0
							cursor_y+
						}
					}Else: InKey($28){
						cursor_y+
						If cursor_y>=ginfo_winy/SIZE_Y{
							cursor_y=ginfo_winy/SIZE_Y-1
						}
					}
				}
				ime_comp="
				s=0
				ImmGetCompositionString hwnd_ime,$800,ime_comp,s
				s=stat
				sdim ime_comp,s+1
				ImmGetCompositionString hwnd_ime,$800,ime_comp,s
				If s==0{
					ss=0
				}
				If s!=0&&ss==0{
					str1_=ime_comp
					ss=1
				}else{
					ime_comp="
				}
				If ime_text==""&&key(243)==0: InKey($20){
					ime_comp="　
				}
				OnDisplay ime_comp
				cursor_y+(cursor_x+StrLen(ime_comp))/(ginfo_winx/SIZE_X)
				cursor_x=(cursor_x+StrLen(ime_comp))\(ginfo_winx/SIZE_X)
			}
			If cursor_y>=ginfo_winy/SIZE_Y{
				cursor_y-
				Repeat MAX_X*(MAX_Y-1)
					data(cnt\MAX_X,cnt/MAX_X)=data(cnt\MAX_X,cnt/MAX_X+1)
				Loop
			}
			Repeat MAX_X*MAX_Y
				work=Peek(data,cnt*4)
				If work{
					If full{
						Print StrF("%c%c",Peek(data,(cnt-1)*4),work)
						full=0
					}Else{
						Pos cnt\MAX_X*SIZE_X,cnt/MAX_X*SIZE_Y
						Color Peek(data,cnt*4+1),Peek(data,cnt*4+2),Peek(data,cnt*4+3)
						If $81<=work&&work<=$9F||($E0<=work&&work<=$FC)&&Peek(data,(cnt+1)*4)!=0{
							full=1
						}Else{
							Print StrF("%c",work)
						}
					}
				}
			Loop
			If run_mode==0{
				If blink\32<16{
					Repeat SIZE_X*SIZE_Y
						pGet cursor_x*SIZE_X+cnt\SIZE_X,cursor_y*SIZE_Y+cnt/SIZE_X
						Color 255-ginfo_r,255-ginfo_g,255-ginfo_b
						pSet cursor_x*SIZE_X+cnt\SIZE_X,cursor_y*SIZE_Y+cnt/SIZE_X
					Loop
				}
				blink+
			}
			ReDraw 1
		}
		Await 0	; コンピュータのばか!!
	Loop

*Check
	programming=0
	ForEach number
		If StrMid(sentence,0,2)==number(cnt){
			programming=1
			Break
		}
	Loop
	reading="
	If programming{
		For i,0,StrLen(sentence),2
			work=0
			ForEach number
				If StrMid(sentence,i,2)==number(cnt){
					reading+number(cnt)
					work=1
					Break
				}
			Loop
			If work==0{
				program(Kan2Arabia(reading)-1)=reading+"，"+StrMid(sentence,i,StrLen(sentence)-i)
				_Break
			}
		Next
	}Else{
		error=1
		For i,0,StrLen(sentence)
			reading+StrMid(sentence,i,1)
			Switch reading
				Case "滅幕"
					Repeat MAX_X*MAX_Y
						data(cnt\MAX_X,cnt/MAX_X)=0
					Loop
					cursor_x=0
					cursor_y=0
					error=0
					SwBreak
				Case "記"
					work=StrMid(sentence,i+1,StrLen(sentence)-(i+1))
					Split work,"「",work
					If stat>1{
						Split work(1),"」",work
						OnDisplay work(0)
						If StrMid(work(1),0,2)=="續"{
							cursor_x+StrLen(work(0))
							If cursor_x>=ginfo_winx/SIZE_X{
								cursor_y+
								cursor_x\(ginfo_winx/SIZE_X)
							}
						}Else{
							cursor_y+
						}
					}Else: If StrMid(work,0,2)=="文"{
						ForEach program
							If program(cnt)!=""{
								OnDisplay program(cnt)
								cursor_y+
							}
						Loop
					}Else{
						cursor_y+
					}
					error=0
					SwBreak
				Case "色是"
					error=0
					SwBreak
				Case "鐘"
					Beep 880,500
					error=0
					SwBreak
				Case "走"
					run_mode=1
					run_line=0
					error=0
					SwBreak
				Case "終"
					run_mode=0
					error=0
					SwBreak
				Case "退"
					End
				Case "去"
					work=StrMid(sentence,i+1,StrLen(sentence)-(i+1))
					run_line=Kan2Arabia(work)-2
					error=0
					SwBreak
			SwEnd
		Next
		If error{
			Beep 880,500
			If run_mode{
				OnDisplay "第"+work_line+"有不正文。
			}Else{
				OnDisplay "有不正文。
			}
			cursor_y+
		}
		If run_mode==0{
			OnDisplay "好。
			cursor_y+
			cursor_x=0
		}
	}
	Return
